plugins {
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

allprojects {
    group 'org.ishugaliy'
    version '1.0.0'

    apply plugin: 'java'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.ishugaliy'
            artifactId = 'allgood-consistent-hash'
            version = '1.0.0'

            from components.java

            pom {
                name = 'AllGood Consisntent Hash'
                description = 'Java implementation of Consistent Hash Ring with Virtual Nodes'
                url = 'https://github.com/ishugaliy/allgood-consistent-hash'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/ishugaliy/allgood-consistent-hash/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'ishugaliy'
                        name = 'Yuriy Shugaliy'
                        email = 'yuriy.shugaliy@gmail.com'
                    }
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/ishugaliy/allgood-consistent-hash/issues'
                }
                scm {
                    connection = 'scm:git:git://github.com/ishugaliy/allgood-consistent-hash.git'
                    developerConnection = 'scm:git:ssh://git@github.com:ishugaliy/allgood-consistent-hash.git'
                    url = 'https://github.com/ishugaliy/allgood-consistent-hash'
                }
            }

        }
    }
    repositories {
        maven {
            name = "MavenCentral"
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username
                password
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'net.openhft', name: 'zero-allocation-hashing', version: '0.11'

    testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.3.3'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'
}